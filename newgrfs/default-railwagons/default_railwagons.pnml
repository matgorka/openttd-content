#define FORLOOP(MACRO, ARG) \
  MACRO(27, ARG)            \
  MACRO(28, ARG)            \
  MACRO(29, ARG)            \
  MACRO(30, ARG)            \
  MACRO(31, ARG)            \
  MACRO(32, ARG)            \
  MACRO(33, ARG)            \
  MACRO(34, ARG)            \
  MACRO(35, ARG)            \
  MACRO(36, ARG)            \
  MACRO(37, ARG)            \
  MACRO(38, ARG)            \
  MACRO(39, ARG)            \
  MACRO(40, ARG)            \
  MACRO(41, ARG)            \
  MACRO(42, ARG)            \
  MACRO(43, ARG)            \
  MACRO(44, ARG)            \
  MACRO(45, ARG)            \
  MACRO(46, ARG)            \
  MACRO(47, ARG)            \
  MACRO(48, ARG)            \
  MACRO(49, ARG)            \
  MACRO(50, ARG)            \
  MACRO(51, ARG)            \
  MACRO(52, ARG)            \
  MACRO(53, ARG)            \
  MACRO(57, ARG)            \
  MACRO(58, ARG)            \
  MACRO(59, ARG)            \
  MACRO(60, ARG)            \
  MACRO(61, ARG)            \
  MACRO(62, ARG)            \
  MACRO(63, ARG)            \
  MACRO(64, ARG)            \
  MACRO(65, ARG)            \
  MACRO(66, ARG)            \
  MACRO(67, ARG)            \
  MACRO(68, ARG)            \
  MACRO(69, ARG)            \
  MACRO(70, ARG)            \
  MACRO(71, ARG)            \
  MACRO(72, ARG)            \
  MACRO(73, ARG)            \
  MACRO(74, ARG)            \
  MACRO(75, ARG)            \
  MACRO(76, ARG)            \
  MACRO(77, ARG)            \
  MACRO(78, ARG)            \
  MACRO(79, ARG)            \
  MACRO(80, ARG)            \
  MACRO(81, ARG)            \
  MACRO(82, ARG)            \
  MACRO(83, ARG)            \
  MACRO(89, ARG)            \
  MACRO(90, ARG)            \
  MACRO(91, ARG)            \
  MACRO(92, ARG)            \
  MACRO(93, ARG)            \
  MACRO(94, ARG)            \
  MACRO(95, ARG)            \
  MACRO(96, ARG)            \
  MACRO(97, ARG)            \
  MACRO(98, ARG)            \
  MACRO(99, ARG)            \
  MACRO(100, ARG)           \
  MACRO(101, ARG)           \
  MACRO(102, ARG)           \
  MACRO(103, ARG)           \
  MACRO(104, ARG)           \
  MACRO(105, ARG)           \
  MACRO(106, ARG)           \
  MACRO(107, ARG)           \
  MACRO(108, ARG)           \
  MACRO(109, ARG)           \
  MACRO(110, ARG)           \
  MACRO(111, ARG)           \
  MACRO(112, ARG)           \
  MACRO(113, ARG)           \
  MACRO(114, ARG)           \
  MACRO(115, ARG)

#define __ARR(I, _)            x##I,
#define __MARK(I, _)           item (FEAT_TRAINS, x##I, I) {}
#define WAGONS_ARRAY()         [ FORLOOP(__ARR, 0) ]
#define MARK_WAGONS_TO_SORT()  FORLOOP(__MARK, 0)

#define APPLY_TO_ALL_RAILS(NMLID, INGAMEID, PROPERTIES) \
item (FEAT_TRAINS, rail_##NMLID, INGAMEID) {            \
  property PROPERTIES                                   \
}                                                       \
                                                        \
item (FEAT_TRAINS, monorail_##NMLID, INGAMEID + 30) {   \
  property PROPERTIES                                   \
}                                                       \
                                                        \
item (FEAT_TRAINS, maglev_##NMLID, INGAMEID + 62) {     \
  property PROPERTIES                                   \
}

#define DISABLE_ON_ALL_RAILS(INGAMEID)    \
disable_item(FEAT_TRAINS, INGAMEID);      \
disable_item(FEAT_TRAINS, INGAMEID + 30); \
disable_item(FEAT_TRAINS, INGAMEID + 62); \

#define __BOOL(N, FLAG, NAME, DESC, DEF_VALUE) \
param N {                                      \
  FLAG {                                       \
    name: string(STR_PARAM_NAME_##NAME);       \
    desc: string(STR_PARAM_DESC_##DESC);       \
    type: bool;                                \
    bit: 0;                                    \
    def_value: DEF_VALUE;                      \
  }                                            \
}

#define BOOL(N, FLAG, NAME, DEF_VALUE) \
  __BOOL(N, FLAG, NAME, NAME, DEF_VALUE)

grf {
  grfid: "\01\16M\00";
  name: string(STR_GRF_NAME);
  desc: string(STR_GRF_DESC);
  url: string(STR_GRF_URL);
  version: 1;
  min_compatible_version: 0;

  BOOL(0, isBugFixOn, BUGFIX, 1)

  param 1 {
    isFoodAndFruitSeparate {
      name: string(STR_PARAM_NAME_SEPARATE);
      desc: string(STR_PARAM_DESC_SEPARATE);
      type: int;
      min_value: 0;
      max_value: 2;
      def_value: 1;
      names: {
        0: string(STR_PARAM_REEFER_DEFAULT);
        1: string(STR_PARAM_SEPARATE);
        2: string(STR_PARAM_SINGLE_WAGON);
      };
    }
  }

  BOOL(2, isRenameOn, RENAME, 0)
}

if (grf_future_status("OG+1")) {
  error(ERROR, string(STR_ERR_OGPT));
}

if (climate != CLIMATE_TOYLAND) {
  if (isBugFixOn) {
    if (climate == CLIMATE_TEMPERATE) {
      APPLY_TO_ALL_RAILS(food, 38, {
        climates_available: ALL_CLIMATES;
        default_cargo_type: 0x0B;
      })

      APPLY_TO_ALL_RAILS(fruit, 42, {
        climates_available: ALL_CLIMATES;
        default_cargo_type: 0x0E;
      })
    } else {
      if (climate == CLIMATE_ARCTIC) {
        APPLY_TO_ALL_RAILS(coal, 29, {
          refittable_cargo_classes: bitmask(CC_BULK);
          cargo_disallow_refit:     [ 0x06 ];
        })
      } else {
        APPLY_TO_ALL_RAILS(copper, 40, {
          refittable_cargo_classes: bitmask(CC_BULK);
          cargo_disallow_refit:     [ 0x06 ];
        })
      }

      APPLY_TO_ALL_RAILS(grain, 33, {
        default_cargo_type: 0x06;
      })
    }
  }

  if (climate == CLIMATE_ARCTIC && isFoodAndFruitSeparate == 1) {
    APPLY_TO_ALL_RAILS(food, 38, {
      cargo_disallow_refit: [ 0x0E ];
    })

    APPLY_TO_ALL_RAILS(fruit, 42, {
      climates_available: ALL_CLIMATES;
      default_cargo_type: 0x0E;
    })
  } else if (climate != CLIMATE_ARCTIC && isFoodAndFruitSeparate == 2) {
    APPLY_TO_ALL_RAILS(food, 38, {
      refittable_cargo_classes: bitmask(CC_REFRIGERATED);
    })

    // disable fruit wagons
    DISABLE_ON_ALL_RAILS(42)
  }

  if (isRenameOn) {
    APPLY_TO_ALL_RAILS(food, 38, {
      name: string(STR_WAGON_NAME_REFRIGERATOR_VAN);
    })
  }

  MARK_WAGONS_TO_SORT()
  sort(FEAT_TRAINS, WAGONS_ARRAY());
}
